// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    Not(in=instruction[15], out=AInstructions);
    Not(in=AInstructions, out=CInstructions);
    Mux16(a=ALUOutput, b=instruction, sel=AInstructions, out=leftMux);
    // Control bit of A Register.
    // You can only write in A if it is a A instruction OR it is a C instruction and dest includes A (shown by d1 (instruction[5])).
    And(a=CInstructions, b=instruction[5], out=loadAInCInstructions);
    Or(a=AInstructions, b=loadAInCInstructions, out=AControl);
    ARegister(in=leftMux, load=AControl, out=AOut, out[0..14]=addressM);
    // Middle Mux.
    Mux16(a=AOut, b=inM, sel=instruction[12], out=AorM);
    // Control bit of D.
    // You can only write in D if it is a C instruction and dest includes D.
    And(a=instruction[4], b=CInstructions, out=DControl);
    DRegister(in=ALUOutput, load=DControl, out=DOut);
    ALU(x=DOut, y=AorM, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=ALUOutput, out=outM, zr=zr, ng=ng);
    // writeM.
    // You can only write in M if it is a C instruction and dest includes M.
    And(a=instruction[3], b=CInstructions, out=writeM);
    // the jump condition of program counter
    // (ng AND j1) OR (zr AND j2) OR (!ng AND !zr AND j3)
    And(a=ng, b=instruction[2], out=ngAndJ1);
    And(a=zr, b=instruction[1], out=zrAndJ2);
    Not(in=ng, out=notNg);
    Not(in=zr, out=notZr);
    And(a=notNg, b=notZr, out=notNgAndNotZr);
    And(a=notNgAndNotZr, b=instruction[0], out=orThird);
    Or(a=ngAndJ1, b=zrAndJ2, out=orFirstTwo);
    Or(a=orFirstTwo, b=orThird, out=jump);
    // Only jump in C instructions
    And(a=CInstructions, b=jump, out=jumpInC);
    Not(in=jumpInC, out=inc);
    PC(in=AOut, load=jumpInC, inc=inc, reset=reset, out[0..14]=pc);
}